*Nb.: work-in-progress, unstable/incomplete.*

if you really want to use, clone and load or use an advanced package manager like [[https://github.com/progfolio/elpaca][Elpaca]].

E.g.,

#+begin_src elisp
(use-package sysinfo-environs
  :ensure (:wait t :host github :repo "emacs-bigtop/sysinfo-environs")
  :demand t
  :config
  (setq this-system-os
        (let ((idlike
               (sysinfo-environs-look-up-field "os-release-info" "ID_LIKE")))
          (if idlike
              idlike
            (sysinfo-environs-look-up-field "os-release-info" "ID")))))
#+end_src

Or Emacs 30+ built-in ~package-vc~:

#+begin_src elisp
(use-package sysinfo-environs
  :vc (sysinfo-environs :url "https://github.com/emacs-bigtop/sysinfo-environs"
                        :branch "main")
  :ensure t
  :config
  (setq this-system-os
        (let ((idlike
               (sysinfo-environs-look-up-field "os-release-info" "ID_LIKE")))
          (if idlike
              idlike
            (sysinfo-environs-look-up-field "os-release-info" "ID")))))
#+end_src

Or quelpa:

#+begin_src elisp
(use-package sysinfo-environs
  :quelpa (sysinfo-environs :fetcher github :repo "emacs-bigtop/sysinfo-environs")
  :ensure t
  :config
  (setq this-system-os
        (let ((idlike
               (sysinfo-environs-look-up-field "os-release-info" "ID_LIKE")))
          (if idlike
              idlike
            (sysinfo-environs-look-up-field "os-release-info" "ID")))))
#+end_src

(See below discussion for why these options.)

* Emacs System Information and Environs
Get information about your Emacs and your system.

Can be used to extract particular bits of information, or to display nice Org-formatted tables of information.

(The idea being something like [[https://github.com/dylanaraps/neofetch][neofetch]] or [[https://github.com/fastfetch-cli/fastfetch][fastfetch]], but for/in Emacs; but also (perhaps more usefully) a nice frontend for programmatically executing Elisp [e.g., in your ~init.el~, do different things depending not only on whether you're on Linux or another OS, but also depending on exactly which Linux distro (or distro family) you're running on] or doing other system-specific things [e.g., setting PDF metadata].)

** temp-buffer-displaying interactive functions
For the latter, currently the following functions are defined and can be used as interactive commands which show information in a temp buffer with Org-mode styling:
- ~sysinfo-environs-os-release-info~: show the ~/etc/os-release~ info (on this information and specification, see: [[https://linux.org/docs/man5/os-release.html][OS_RELEASE(5)]])
- ~sysinfo-environs-os-uname-info-display~: show various information that ~uname~ knows about (on ~uname~, see [[https://en.wikipedia.org/wiki/Uname][here]])
- ~sysinfo-environs-os-release-and-uname-display~: show both of the previous 
- ~sysinfo-environs-emacs-self-info-display~: show information about your Emacs (version, toolkit &c.)
- ~sysinfo-environs-full-sys-info-display~: show all the information that Emacs System Information and Environs can display. [currently this is all of the above, plus what very basic system information Emacs knows about your OS]. this is encoded in variable ~sysinfo-environs-dataset-alist~.

Since ~/etc/os-release~ defines a field ~LOGO~ which can be specified with the name of the icon/image for the distro, the function ~sysinfo-environs-sysinfo~ tries to find the appropriate image file (in the best resolution, the ~.svg~ if possible) on your system, and any interactive functions which display system information involving the information from ~/etc/os-release~ will try to display this image in the temp buffer:

[[./screenshots/sysinfo-environs_guix_ex.jpg]]

[Currently, ~sysinfo-environs-sysinfo~ tries in order:
#+begin_src elisp
'("/usr/local/share/icons" ;; if you've a local thing
  (concat "/usr/share/" os-id-name) ;; e.g. /usr/share/endeavouros
  "/usr/share/pixmaps" ;; another place they might be
  "/run/current-system/profile/share/icons" ;; where they are on Guix
                                            ;; (& maybe Nix?)
  "/usr/share/icons") ;; usual place
#+end_src

[[https://github.com/emacs-bigtop/sysinfo-environs/issues/new/choose][Open an issue]] if you're on a system where there should be system/distro/os icons and they're somewhere else and so  ~sysinfo-environs-sysinfo~ isn't finding them.]

Another screenshot, showing more system information:
[[./screenshots/sysinfo-environs_full-ex.jpg]]

** Building neofetch/fastfetch clone in elisp
The beginnings of a neofetch/fastfetch function are to be found by calling ~sysinfo-environs-neofetch-like~. Some example screenshots below, with commentary.

*** on [[https://endeavouros.com][EndeavourOS]] with [[https://stumpwm.github.io][StumpWM environment]]
#+CAPTION: on [[https://endeavouros.com][EndeavourOS]] with [[https://stumpwm.github.io][StumpWM environment]]
#+LABEL: endeavourstumpwm
[[./screenshots/endeavour-neofetch-emacs.png]]

*** on [[https://alpinelinux.org][Alpine Linux]] with [[https://github.com/ch11ng/exwm][EXWM environment]]
#+CAPTION: on [[https://alpinelinux.org][Alpine Linux]] with [[https://github.com/ch11ng/exwm][EXWM environment]]
#+LABEL: alpineexwm
[[./screenshots/alpine-(n)E(o)fetch.png]]

*** on [[https://guix.gnu.org][Guix]] with [[https://stumpwm.github.io][StumpWM environment]]
#+CAPTION: on [[https://guix.gnu.org][Guix]] with [[https://stumpwm.github.io][StumpWM environment]]
#+LABEL: guixstumpwm
[[./screenshots/guix-(n)E(o)fetch.jpg]]

We plan to add more information at least about interesting Emacs build features (nativecomp, puregtk, &c.), as well as more system information.

*** on [[https://cachyos.org][CachyOS]] with [[https://kde.org/plasma-desktop][KDE Plasma 6 (Wayland) environment]]
Also, still need to figure out rational fallbacks or other values to check on things like custom-built desktops, because right now some fields are not filled in very satisfactorily (and fastfast manages better on these cases).....

#+CAPTION: on [[https://cachyos.org][CachyOS]] with [[https://kde.org/plasma-desktop][KDE Plasma 6 (Wayland) environment]]
#+LABEL: cachyplasmawaydesk
[[./screenshots/cachyos-(n)E(o)fetch.jpg]]

*** on [[https://secureblue.dev][secureblue]] with [[https://swaywm.org][Sway environment]] (wayland)
And in some places, the ~LOGO~ is not a reliable guide to the actual LOGO-IMAGE, as for [[https://secureblue.dev][secureblue]], where the =os-release= specified ~LOGO~ is "fedora-logo-icon", but none of the standard paths (we currently search =/usr/local/share/icons/=, =/usr/share/<distro-name>=, =/usr/share/pixmaps=, =/run/current-system/profile/share/icons= (for Guix), and =/usr/share/icons=).

#+CAPTION: on [[https://secureblue.dev][secureblue]] with [[https://swaywm.org][Sway environment]] (wayland)
#+LABEL: securebluesway
[[./screenshots/neofetch+sysinfo-secureblue-emacs.png]]

But in this case what appears to be the right logo icon is actually located at =/usr/share/pixmaps/fedora-logo-sprite.svg=.  (Currently in an unpushed branch, where we currently just have a hack to find the right icon for secureblue....)

** ~sysinfo-environs-look-up-field~ and your ~init.el~
For particular pieces of information, you can use the ~sysinfo-environs-look-up-field~ function, either in Elisp or as an interactive command.

When ~sysinfo-environs-look-up-field~ it will ask the user to choose between the datasets in ~sysinfo-environs-dataset-alist~ and then show a list of fields from the dataset and ask the user to choose one of these and then message the value of that field in the message area/echo buffer.

When called in Elisp, ~sysinfo-environs-look-up-field~ can be passed just the dataset, in which case it behaves interactively, as above (just not asking again for the dataset, but still asking the user to choose a field) - and the dataset can be passed by either its name in ~sysinfo-environs-dataset-alist~ (e.g., ~"uname-info"~ or the function itself (e.g., ~(sysinfo-environs-parse-uname-info)~).

Or else  ~sysinfo-environs-look-up-field~ can be passed both dataset and field name, and it will return the value of that field in that dataset:

E.g.,

#+begin_src elisp
(sysinfo-environs-look-up-field "emacs-info" "system-type") ;; =
;; gnu/linux
(sysinfo-environs-look-up-field (sysinfo-environs-emacs-known-sysinfo) "system-type") ;; =
;; gnu/linux
(sysinfo-environs-look-up-field "emacs-self-info" "toolkit") ;; =
;; "Lucid/Athena-toolkit (X toolkit)"
(sysinfo-environs-look-up-field "uname-info" "KERNEL_RELEASE") ;; =
;; "6.13.5"
(sysinfo-environs-look-up-field "os-release-info" "ID") ;; =
;; "guix"
#+end_src

*** Use 1: cross-distro shared ~init.el~ files
This last example is one of the prompting factors for this package: it can be useful to know not only if the current instance of Emacs is running on Linux (rather than, e.g., Windows) but also /which distro/ it's running under. Because you might have a single Emacs ~init.el~ that you use across many systems and want to keep synchronised (say, with [[https://syncthing.net][Syncthing]]) in order to have changes you make to your ~init.el~ propagate across all of your Emacs instances on various machines, but you might some things to work differently on different machines depending on the particulars of the machine. Which in some cases might include which distro Emacs is running under. (In the past I've just used ~(system-name)~ for this and "hard-coded" things by knowing that the computers called "my-guix-laptop" and "old-thinkpad02" are both running on [[https://guix.gnu.org][Guix]].)

There are two very useful values in =/etc/os-release=, and thus accessible by ~sysinfo-environs-look-up-field~, namely ~ID~ and ~ID_LIKE~.

So on a Guix machine, if you call ~(sysinfo-environs-look-up-field "os-release-info" "ID")~, you'll get back ~"guix"~ . If you're running CachyOS, ~(sysinfo-environs-look-up-field "os-release-info" "ID")~ will give you back ~"cachyos"~ and ~(sysinfo-environs-look-up-field "os-release-info" "ID_LIKE")~ will give you back ~"arch"~ - because CachyOS is an [[https://archlinux.org][Arch Linux]] derivative. It might be useful to know the particular variant/derivative you're running Emacs under (e.g., CachyOS), but you might just want to know "is this essentially an Arch machine, whether it's pure Arch or [[https://cachyos.org][CachyOS]] or [[https://endeavouros.com][EndeavourOS]] or [[https://garudalinux.org][Garuda]] &c.?"

If you're going to use it like this, make sure to =require= ~sysinfo-environs~ early in your =init.el= (i.e., before you're going to call on it.)

If you're using a package manager like Elpaca, you'll want to use ~:ensure (:wait t) :demand t~, e.g.:

#+begin_src elisp
(use-package sysinfo-environs
  :ensure (:wait t :host github :repo "emacs-bigtop/sysinfo-environs")
  :demand t
  :config
  (setq this-system-os
        (sysinfo-environs-look-up-field "os-release-info" "ID")))
#+end_src

And so, as in the above config example, you could set a variable which gives you the distro that Emacs is running under, and so you could do something like:

#+begin_src elisp
 (use-package pdf-tools
      :ensure (if (string= this-system-os "guix") nil t)
      :config
      (pdf-tools-install 'no-query))
#+end_src

(This example is for one of my use-cases: some of us (=emacsomancer=) generally want to use Elpaca to manage packages across all of my machines, regardless of which particular OS they're running, but on Guix, he havs to use Guix's own packaged version of ~emacs-pdf-tools~ because [[https://pdftools.wiki][PDF Tools]] wants to install other packages and then compile itself and this won't work on Guix, but works on, e.g., Arch. So here he uses ~use-package~ to ~require~ PDF Tools, but only try to have Elpaca install PDF Tools itself (rather than relying on the OS to already have installed it) when the machine *isn't* running on Guix.)

Because of the difference between the ~os-release~ ~ID~ and ~ID_LIKE~ values, and the fact that not all distros will set ~ID_LIKE~, you might want to set your "this-system-os" variable (whatever you want to call it) like this:

#+begin_src elisp
(setq this-system-os
      (let ((idlike
             (sysinfo-environs-look-up-field "os-release-info" "ID_LIKE")))
        (if idlike
            idlike
          (sysinfo-environs-look-up-field "os-release-info" "ID"))))
#+end_src

This will set ~this-system-os~ to the value of ~ID_LIKE~ if that is defined, and to the value of ~ID~ otherwise. This means ~this-system-os~  will be ~"guix"~ on your Guix machine and ~"arch"~ on both your vanilla Arch machine and your CachyOS machine.

*** Use 2: encode system details
What if you want to encode some system information in your LaTeX-produced PDF?

There's a number of template/completion packages; emacsomancer's been using [[https://github.com/minad/tempel][TempEl]], with a ~templates~ file located at =~/.emacs.d/templates=. So something like (but see TempEl's documentation for the config) in your =init.el=:

#+begin_src elisp
(use-package tempel
  :ensure t
  ;; Require trigger prefix before template name when completing.
  ;; :custom        
  ;; (tempel-trigger-prefix "<")    
  
  :bind (("M-+" . tempel-complete) ;; Alternative tempel-expand
         ("M-*" . tempel-insert))

  :init
  (setq tempel-path "~/.emacs.d/templates")
  ;; Setup completion at point
  (defun tempel-setup-capf ()
    ;; Add the Tempel Capf to `completion-at-point-functions'.
    ;; `tempel-expand' only triggers on exact matches. Alternatively use
    ;; `tempel-complete' if you want to see all matches, but then you
    ;; should also configure `tempel-trigger-prefix', such that Tempel
    ;; does not trigger too often when you don't expect it. NOTE: We add
    ;; `tempel-expand' *before* the main programming mode Capf, such
    ;; that it will be tried first.
    (setq-local completion-at-point-functions
                (cons #'tempel-expand
                      completion-at-point-functions)))

  (add-hook 'prog-mode-hook 'tempel-setup-capf)
  (add-hook 'text-mode-hook 'tempel-setup-capf)

  ;; Optionally make the Tempel templates available to Abbrev,
  ;; either locally or globally. `expand-abbrev' is bound to C-x '.
  ;; (add-hook 'prog-mode-hook #'tempel-abbrev-mode)
  ;; (global-tempel-abbrev-mode)
  )

;; Optional: Add tempel-collection.
;; The package is young and doesn't have comprehensive coverage.
(use-package tempel-collection
  :ensure t)
#+end_src

And then in my =~/.emacs.d/templates= file:
#+begin_src elisp
;; <other shortcuts here>

;; define shortcuts for `tex-mode' (including `hyperrefv')
tex-mode
;; `hyperrefv' is the template shortcut; hit `TAB' key to expand:
(hyperrefv
(let ((nativecomp (if (cl-search "NATIVE_COMP" system-configuration-features) t nil))
      (puregtk (if (cl-search "PGTK" system-configuration-features) t nil)))
 ;; TeX chokes on _
 (replace-regexp-in-string
  "_" "＿"
  ;; TeX doesn't like # either
  (replace-regexp-in-string
   "#" "\\\\#"
   ;; use TeX-specified \title and \author, and then set `creator'
   (concat
    ;; make a "\" TeX-command prefix
    (make-string 1 ?\\)
    "usepackage[pdfusetitle,pdfcreator={"
    ;; Texlive version information
    (car (string-lines (shell-command-to-string "tex --version")))
    ;; Emacs specs
    " using GNU Emacs "
    (sysinfo-environs-look-up-field "emacs-self-info" "emacs-version")
    " (build "
    (sysinfo-environs-look-up-field "emacs-self-info" "emacs-build-number")
    ;; if there is a build-date
    (let ((eb-date (sysinfo-environs-look-up-field "emacs-self-info" "emacs-build-time")))
      (when eb-date
        (concat " [of " eb-date "]")))
    ", "
    (sysinfo-environs-look-up-field "emacs-self-info" "toolkit")
    ;; puregtk
    (when puregtk
       " PureGTK build")
    ;; if there is a scrollbars toolkit
    (let ((scrollb (sysinfo-environs-look-up-field "emacs-self-info" "scrollbars-toolkit")))
      (when (string= scrollb "none")
        (setq scrollb "no"))
      (when scrollb
        (concat ", " scrollb " scroll bars")))
    ;; if there is cairo
    (let ((cairov (sysinfo-environs-look-up-field "emacs-self-info" "cairo-version")))
      (when cairov
      (concat ", cairo version " cairov)))
    (when nativecomp
     ", with native compilation")
    ")"
    " with AUCTeX "
    AUCTeX-version
    ;; (pkg-info-package-version 'auctex) ;; = old way of getting auctex version
    " on "
    ;; OS/distro name:
    (sysinfo-environs-look-up-field "os-release-info" "PRETTY_NAME")
    " ("
    ;; kernel name (e.g., "Linux"):
    (sysinfo-environs-look-up-field "uname-info" "KERNEL_NAME")
    " "
    ;; kernel version:
    (sysinfo-environs-look-up-field "uname-info" "KERNEL_RELEASE")
    " ["
    ;; other kernel specs:
    (sysinfo-environs-look-up-field "uname-info" "KERNEL_SPECS")
    ;; called as arguments to TeX package `hyperref'
    "])}]{hyperref}")))))
   #+end_src

And then in a LaTeX document, somewhere in the preamble type ~hyperrefv~ and then hit ~TAB~ and it expands into something like:

#+begin_src tex
\usepackage[pdfusetitle,pdfcreator={TeX 3.141592653 (TeX Live
2026/dev/Arch Linux) using GNU Emacs 30.2 (build 2 [of 2025-09-10
13:32:34], Lucid/Athena-toolkit (X toolkit), Xaw3d scroll bars,
cairo version 1.18.4) with AUCTeX 14.1.0 on CachyOS (Linux
6.18.0-3-cachyos [\#1 SMP PREEMPT＿DYNAMIC Sun, 07 Dec 2025
09:39:03 +0000])}]{hyperref}
#+end_src

Something similar would work with other template/completion packages like [[https://github.com/joaotavora/yasnippet][yasnippet]].

(The above LaTeX might produce a PDF that has metadata like this:

[[./screenshots/pdffonts-etc-hyperref-ex2.jpg]]

[output from [[https://github.com/emacsomancer/pdffontetc][PDF Fonts Etc.]]])

Note, the updated code above also checks for native compilation and PureGTK builds, thus will now produce things like (note ~PureGTK build~ and ~with native compilation~ in the text):

#+begin_src tex
\usepackage[pdfusetitle,pdfcreator={TeX 3.141592653 (TeX Live
2025/Homebrew) using GNU Emacs 30.2 (build 1 [of 2025-11-14
17:02:42], GTK-toolkit (GTK 3.24.51) PureGTK build, no scroll
bars, cairo version 1.18.4, with native compilation) with AUCTeX
14.1.0 on secureblue (powered by Fedora Atomic) (Linux
6.17.12-300.fc43.x8664 [\#1 SMP PREEMPTDYNAMIC Sat Dec 13
05:06:24 UTC 2025])}]{hyperref}
#+end_src


** Other notes
.....
